@echo off

echo [34mChecking for windows updates...[0m
echo This can take some time stand by...
call sudo Get-WindowsUpdate
call sudo Install-WindowsUpdate -AcceptAll -IgnoreReboot
echo [32mWindows update compleat...[0m


:: If WSL is detected update:
IF EXIST %WINDIR%\system32\bash.exe (
    echo.
    echo [34mWindows Subsystem for Linux detected...[0m
    echo Preforming full upgrade...
    call bash.exe -c "sudo apt update && sudo apt full-upgrade -y && sudo apt autoremove -y; sudo snap refresh;";
)
:: Workaroudn to get colors to work
IF EXIST %WINDIR%\system32\bash.exe ( echo [32mWindows Subsystem for Linux update compleat...[0m )


:: If Scoop is detected update:
IF EXIST %USERPROFILE%\scoop\shims\scoop (
    echo.
    echo [34mScoop detected...[0m
    IF NOT EXIST %USERPROFILE%\scoop\apps\sudo\current\sudo.ps1 (
        IF NOT EXIST %PROGRAMDATA%\scoop\apps\sudo\current\sudo.ps1 (
            IF NOT EXIST %PROGRAMDATA%\chocolatey\bin\Sudo.exe (
                IF NOT EXIST %USERPROFILE%\.scripts\sudo.ps1 GOTO SUDO_MISSING
            )
        )
    )
    echo Updating repository...
    call sudo scoop update
    echo Updating local packages...
    call sudo scoop update *
    echo Updating globla packages...
    call sudo scoop update * --global
    echo [32mScoop update compleat...[0m
)

:: If CHOCO is detected update:
IF EXIST %ChocolateyInstall%\choco.exe (
    echo.
    echo [34mChocolatey detected...[0m
    IF NOT EXIST %PROGRAMDATA%\chocolatey\bin\Sudo.exe (
        IF NOT EXIST %USERPROFILE%\scoop\apps\sudo\current\sudo.ps1 (
            IF NOT EXIST %PROGRAMDATA%\scoop\apps\sudo\current\sudo.ps1 (
                IF NOT EXIST %USERPROFILE%\.scripts\sudo.ps1 GOTO SUDO_MISSING
            )
        )
    )
    echo Updating packages...
    call sudo choco upgrade all -y
    echo [32mChocolatey update compleat...[0m
)

GOTO END
:: If SUDO dont exist tell to install
:SUDO_MISSING
    echo [93mPackage dependency does not exist 'sudo' please install it or run in a elevated terminal:[0m
    IF EXIST %USERPROFILE%\scoop\shims\scoop (echo    [93mscoop install sudo[0m)
    IF EXIST %ChocolateyInstall%\choco.exe (echo    [93mchoco install sudo[0m)
    echo [31mScript aborted.[0m
    exit 1
:END

echo [32mAll updates is completed.[0m

echo.
exit 0
